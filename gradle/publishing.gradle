apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

ext.website = 'https://radar-base.org'
ext.githubUrl = 'https://github.com/RADAR-base/RADAR-HDFS-Sink-Connector'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        RadarPublication(MavenPublication) {

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            pom {
                name = 'radar-hdfs-sink-connector'
                description = 'A HDFS Sink connector for RADAR-CNS platform.'
                url = 'https://github.com/RADAR-CNS/RADAR-HDFS-Sink-Connector.git'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'radar-cns'
                        name = 'Nivethika'
                        email = 'nivethika@thehyve.nl'
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = githubUrl + '/issues'
                }
                organization {
                    name = 'RADAR-base'
                    url = website
                }
                scm {
                    connection = 'scm:git:' + githubUrl
                    url = githubUrl
                }
            }
        }
    }
}



bintray {
    user System.getenv('BINTRAY_USER')? System.getenv('BINTRAY_USER') : project.property('bintrayUser')
    key System.getenv('BINTRAY_API_KEY') ? System.getenv('BINTRAY_API_KEY') : project.property('bintrayApiKey')
    override false
    publications 'RadarPublication'
    pkg {
        repo = 'org.radarcns'
        name = 'radar-hdfs-sink-connector'
        userOrg = 'radar-cns'
        licenses = ['Apache-2.0']
        vcsUrl = githubUrl
        version {
            name = project.version
            desc = 'RADAR-HDFS-Sink-Connector'
            vcsTag = System.getenv('TRAVIS_TAG')
        }
    }
}
